include "mem.nhp"

mod data:
	class list<T>:
		readonly T defaultValue
		array<T> buffer
		int count
		
		def __init__(int capacity, T defaultValue):
			self.buffer = new T[capacity](self.defaultValue = defaultValue)
			self.count = 0
		
		def pushBack(T elem):
			if self.count == (self.buffer as int):
				newBuffer = new T[self.count + 10](self.defaultValue)
				mem::memcpy(newBuffer, self.buffer, 0, 0, self.count)
				self.buffer = newBuffer
			self.buffer[self.count] = elem
			self.count = self.count + 1
		def addRange(list<T> range):
			if self.count + range.count >= self.buffer as int:
				newBuffer = new T[self.count + range.count](self.defaultValue)
				mem::memcpy(newBuffer, self.buffer, 0, 0, self.count)
				self.buffer = newBuffer
			mem::memcpy(self.buffer, range.buffer, self.count, 0, range.count)
			self.count = self.count + range.count
		def remove(fn<bool, T> toRemove) bool:
			i = 0
			while i < self.count:
				if toRemove(self.buffer[i]):
					mem::memcpy(self.buffer, self.buffer, i, i + 1, self.count - (1 + i))
					return True
				i = i + 1
			return False
		
		def toArray() array<T>:
			buffer = new T[self.count](self.defaultValue)
			i = 0
			while i < self.count:
				buffer[i] = self.buffer[i]
				i = i + 1
			return buffer
		
		def getAtIndex(int index) T:
			if index > self.count:
				abort "Index Over Range"
			elif index < 0:
				abort "Index Under Range"
			return self.buffer[index]
		
		def setAtIndex(int index, T elem) T:
			if index > self.count:
				abort "Index Over Range"
			elif index < 0:
				abort "Index Under Range"
			return self.buffer[index] = elem
		
		def forall(fn<nothing, T> todo):
			i = 0
			while i < self.count:
				todo(self.buffer[i])
				i = i + 1
		def sort(fn<int, T, T> compare):
			sorted = False
			while sorted == False:
				sorted = True
				i = 1
				while i < self.count:
					if compare(self.buffer[i - 1], self.buffer[i]) > 0:
						temp = self.buffer[i]
						self.buffer[i] = self.buffer[i - 1]
						self.buffer[i - 1] = temp
						sorted = False
					i = i + 1
		
		def map(fn<T, T> transform) list<T>:
			mapped = new list<T>(self.count, self.defaultValue)
			i = 0
			while i < self.count:
				mapped.pushBack(transform(self.buffer[i]))
				i = i + 1
			return mapped
		def filter(fn<bool, T> keepCond) list<T>:
			filtered = new list<T>(self.count, self.defaultValue)
			i = 0
			while i < self.count:
				if keepCond(self.buffer[i]):
					filtered.pushBack(self.buffer[i])
				i = i + 1
			return filtered
		def reduce(fn<T, T, T> add) T:
			accum = getAtIndex(0)
			i = 1
			while i < self.count:
				accum = add(accum, self.buffer[i])
				i = i + 1
			return accum