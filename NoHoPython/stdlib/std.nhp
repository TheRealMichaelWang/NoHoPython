cinclude "<stdlib.h>"
cinclude "<math.h>"

enum nullable<T>:
	T
	None

class pair<A, B>:
	A first
	B second
	def __init__(A first, B second):
		self.first = first
		self.second = second

enum numerical:
	dec
	int

mod std:
	mod cinterop:
		cdef acos(dec) dec
		cdef asin(dec) dec
		cdef atan(dec) dec
		cdef sin(dec) dec
		cdef cos(dec) dec
		cdef tan(dec) dec
		cdef exit(int) None
		cdef atan2(dec, dec) dec
		cdef atof(handle) dec
		cdef atol(handle) int
		cdef fabs(dec) dec
		cdef abs(int) int
	mod __utils__:
		def numToDec(numerical num) dec:
			match num:
				dec d:
					return d
				int i:
					return i as dec
	
	def exit(int status):
		cinterop::exit(status)
	
	def abs(numerical num) numerical:
		match num:
			dec d:
				return cinterop::fabs(d)
			int i:
				return cinterop::abs(i)
	
	def sin(numerical num) dec:
		return cinterop::sin(__utils__::numToDec(num))
	def cos(numerical num) dec:
		return cinterop::cos(__utils__::numToDec(num))
	def tan(numerical num) dec:
		return cinterop::tan(__utils__::numToDec(num))
	def asin(numerical num) dec:
		return cinterop::asin(__utils__::numToDec(num))
	def acos(numerical num) dec:
		return cinterop::acos(__utils__::numToDec(num))
	def atan(numerical num) dec:
		return cinterop::atan(__utils__::numToDec(num))
	def atan2(numerical n, numerical d) dec:
		return cinterop::atan2(__utils__::numToDec(n), __utils__::numToDec(d))
	
	def fmax(dec a, dec b) dec:
		return a if a > b else b
	def fmin(dec a, dec b) dec:
		return a if a < b else b
	
	def forall<T>(array<T> buf, fn<None, T> todo):
		i = 0
		while i < buf as int:
			todo(buf[i])
			i = i + 1
	def repeat(int n, fn<None, int> todo):
		i = 0
		while i < n:
			todo(i)
			i = i + 1
	def repeatStepped(int start, int stop, int step, fn<None, int> todo):
		i = start
		while i != stop:
			todo(i)
			i = i + step
	
	def reduce<T>(array<T> a, fn<T, T, T> add) T:
		accum = a[0]
		i = 1
		while i < a as int:
			accum = add(accum, a[i])
			i = i + 1
		return accum